plugins {
    id 'org.sonarqube' version "2.7"
    id 'jacoco'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    repositories {
        jcenter()

        maven {
            url 'https://repository.klauke-enterprises.com/repository/maven-snapshots/'
            mavenContent {
                snapshotsOnly()
            }
        }
        maven {
            url 'https://repository.klauke-enterprises.com/repository/maven-releases/'
            mavenContent {
                releasesOnly()
            }
        }
    }

    dependencies {
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.4.2'
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.4.2'
    }

    test {
        useJUnitPlatform()
    }
}

sonarqube {
    properties {
        property "sonar.jacoco.reportPaths", "build/jacoco/test.exec"
        property "sonar.login", "$System.env.SONAR_LOGIN"
        property "sonar.host.url", "$System.env.SONAR_URL"
        property "sonar.projectKey", "$System.env.SONAR_PROJECT_KEY"
    }
}

task codeCoverageReport(type: JacocoReport, group: "Coverage reports") {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    
    dependsOn subprojects*.test

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled = true
        xml.destination = file("${buildDir}/reports/jacoco/report.xml")
        html.enabled = true
        html.destination file("${buildDir}/reports/jacoco/html")
        csv.enabled = true
        csv.destination file("${buildDir}/reports/jacoco/csv")
    }
}
